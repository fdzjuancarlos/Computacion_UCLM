import java_cup.runtime.*;
import java.io.*;

parser code {:
  public static void main(String args[]) throws Exception {
    try{
      new parser(new Yylex(System.in)).parse();  // Inicializamos el scanner (Yylex) para que
    }
    catch ( Exception e) {
      System.out.println( e );
      System.out.println(" Análisis INCORRECTO !!");
      System.exit(1);
    }
    System.out.println("Análisis Correcto ");
   }
:}

terminal CORCHETE_ABIERTO, CORCHETE_CERRADO,PARENTESIS_ABIERTO, PARENTESIS_CERRADO, COMA, PUNTO_Y_COMA, ASIGNADOR, UNION, PALABRA_VACIA, IGUAL;
terminal GRAMATICA, TERMINAL, NO_TERMINAL, AXIOMA, PRODUCCION, AUTOMATA;
terminal  ESTADOS, ALFABETO, INICIAL, FINAL, TRANSICION;
terminal TEXTO;


non terminal lista_simbolos_espacio, lista_simbolos_coma, union_lista_simbolos;
non terminal estado_actual_transita, estado_transitado;
non terminal regla_produccion, lista_reglas_produccion;
non terminal regla_transicion,regla_transicion_p, lista_reglas_transicion;
non terminal term_p, no_term_p, axioma_p, produccion_p;
non terminal estados_p, alfabeto_p, inicial_p, final_p, transicion_p;
non terminal term, no_term, axioma, produccion;
non terminal estados, alfabeto, inicial, final, transicion;
non terminal gramat;
non terminal automaton;
non terminal general;

precedence left COMA;
precedence left UNION;
precedence left IGUAL;
precedence left ASIGNADOR;

general ::= automaton general | gramat general | automaton | gramat;



gramat ::= GRAMATICA TEXTO CORCHETE_ABIERTO
   term_p
   no_term_p
   axioma_p
   produccion_p
   CORCHETE_CERRADO;
   
automaton ::= AUTOMATA TEXTO CORCHETE_ABIERTO
   estados_p
   alfabeto_p
   inicial_p
   final_p
   transicion_p
   CORCHETE_CERRADO;
   


term_p ::= term PUNTO_Y_COMA;
no_term_p ::= no_term PUNTO_Y_COMA;
axioma_p ::= axioma PUNTO_Y_COMA;
produccion_p ::= produccion;

estados_p ::= estados PUNTO_Y_COMA;
alfabeto_p ::= alfabeto PUNTO_Y_COMA;
inicial_p ::= inicial PUNTO_Y_COMA;
final_p ::= final PUNTO_Y_COMA;
transicion_p ::= transicion;

term ::= TERMINAL lista_simbolos_coma;
no_term ::= NO_TERMINAL lista_simbolos_coma;
axioma ::= AXIOMA TEXTO;
produccion ::= PRODUCCION CORCHETE_ABIERTO lista_reglas_produccion CORCHETE_CERRADO;

estados ::= ESTADOS lista_simbolos_coma;
alfabeto ::= ALFABETO lista_simbolos_coma;
inicial ::= INICIAL TEXTO;
final ::= FINAL lista_simbolos_coma;
transicion ::= TRANSICION CORCHETE_ABIERTO lista_reglas_transicion CORCHETE_CERRADO;

regla_produccion::= TEXTO ASIGNADOR union_lista_simbolos PUNTO_Y_COMA;
lista_reglas_produccion::= lista_reglas_produccion regla_produccion | regla_produccion;

estado_actual_transita ::= TEXTO COMA | TEXTO COMA TEXTO;
estado_transitado ::= TEXTO | PARENTESIS_ABIERTO lista_simbolos_coma PARENTESIS_CERRADO;
regla_transicion::= estado_actual_transita IGUAL estado_transitado;
regla_transicion_p ::= regla_transicion PUNTO_Y_COMA;
lista_reglas_transicion::= lista_reglas_transicion regla_transicion_p | regla_transicion_p;

lista_simbolos_espacio ::= lista_simbolos_espacio TEXTO | TEXTO;
lista_simbolos_coma ::= lista_simbolos_coma COMA TEXTO | TEXTO;
union_lista_simbolos ::= union_lista_simbolos UNION lista_simbolos_espacio |
                         union_lista_simbolos UNION PALABRA_VACIA |
                         lista_simbolos_espacio;
                         

